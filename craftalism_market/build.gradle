plugins {
    id 'java'
    id "io.github.goooler.shadow" version "8.1.8" // Correct Shadow plugin ID
}

group = 'io.github.HenriqueMichelini'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT") // Paper API
    compileOnly files("libs/craftalism_economy-1.0-SNAPSHOT.jar") // Changed from implementation
    implementation "dev.triumphteam:triumph-gui:3.1.11"

}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

// Configure Shadow plugin
shadowJar {
    relocate("dev.triumphteam.gui", "io.github.HenriqueMichelini.craftalism_market.shaded.gui") // Relocate TriumphGUI
    archiveClassifier.set('') // Remove the "-all" suffix from the JAR file
    minimize() // Optional: Minimize the JAR by removing unused classes (use with caution)
}

tasks.build {
    dependsOn(shadowJar) // Ensure shadowJar is run during the build task
}